buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.marklogic:marklogic-unit-test-client:1.1.0"
    }
}

plugins {
    id "com.marklogic.ml-gradle" version "4.3.2"
    id "com.github.node-gradle.node" version "3.1.1"
    id "base"
}

repositories {
    mavenCentral()
    maven {
        url nodeDistributionBaseUrl;
        allowInsecureProtocol = true
    }
}

dependencies {
    mlBundle "com.marklogic:marklogic-unit-test-modules:1.1.0"
}

def nodeModulesDir = "${project.projectDir}/src/main/ml-modules/node_modules"
node {
    // Version of node to use.
    version = '14.15.4'

    // // Version of npm to use.
    npmVersion = '6.14.10'

    download = true

    // distBaseUrl is not set here; this instead relies on the nodeDistributionBaseUrl repository
    // that is configured in the root build.gradle file

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/npm")

    // Set the work directory where node_modules should be located

}
task clearNodeModules() {
    doLast {
        delete fileTree("${nodeModulesDir}").visit { FileVisitDetails details ->
            delete details.file
        }
    }
}

task createNodeModulesDirectory(dependsOn: clearNodeModules) {
    doLast {
        project.mkdir "${nodeModulesDir}"
    }
}

task npmInstallGraphQl(type: NpmTask, dependsOn: createNodeModulesDirectory) {
    workingDir = file("${nodeModulesDir}")
    args = ['install', 'graphql', '--save-dev']
}

task lint(type: NpmTask, dependsOn: npmInstall) {
    args = ['run', 'lint']

}

mlLoadModules.dependsOn(npmInstallGraphQl)
mlDeployApp.dependsOn(npmInstallGraphQl)
mlUnitTest.dependsOn(mlLoadModules)